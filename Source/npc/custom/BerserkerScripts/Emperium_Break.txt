prontera,140,182,5	script	Emperium Breaker	1288,{

// What Map will be used
set .Map$,"job3_arch01";
// How many Top Breaker will be Recorded
set .TopRank,5;
set .aNameNPC$, "[^0000FF Emperium Test ^000000]";
// Message to Inform Players while inside the Room
setarray .Instruction$[0],
			"Esta sala es para probar que tan rapido puedes romper un Emperium.",
			"Recuerda que este Emperium es una representacion del que aparece en WoE",
			"Sera mejor que te prepares para romperlo, aparecera en el centro de la sala";

while( 1 ){
	mes .aNameNPC$;
	mes "Esta es la sala prueba de Romper el Emperium. ";
	mes "Puedes calcular que tan rapido rompes el Emperium en esta sala.";
	next;
	mes .aNameNPC$;
	switch( select( ( getmapusers( .Map$ ) )?"^FF0000No disponible":"^0000FFDisponible^000000",
				"Top ^FF0000"+.TopRank+"^000000 Record",
				( getgmlevel() < 90 )?"":"^FF0000[GM]^000000 Reset Room",
				( getgmlevel() < 90 )?"":"^FF0000[GM]^000000 Reset Ladder" )){
		Case 1:
			if( getmapusers( .Map$ ) ){
				mes "Hay otro jugador, espera a que termine por favor.";
				next;
				break;
			}
			warp .Map$,0,0;
			killmonster .Map$,"All";
			removemapflag .Map$,mf_noskill;
			donpcevent strnpcinfo(0)+"::OnReady";
			end;
		Case 2:
			mes "^ED1ADCTu ultimo record:^000000 : "+LatestRecord/1000+"."+LatestRecord%1000+" segundos.";
			callfunc "F_GetBest5RompeEmpe";
			next;
		break;

		Case 3:
			mapannounce .Map$,"El GM reinicio los record del Emperium Breaker.",0,0x00FF00;
			killmonster .Map$,"All";
			sleep2 3000;
			mapwarp .Map$,"prontera",156,188;
			mes .aNameNPC$;
			mes "Listo, los jugadores que estaban adentro son enviados a fuera.";
			next;
		break;
		Case 4:	
			switch( select( "^0000FFAll Player^000000:^FF0000One Player^000000" ) ){
				Case 1:
					callfunc "F_ResetRecords";
					mes "RESETED WHOLE LADDER.";
					break;
				Case 2:
					mes "Please input the ^FF0000EXACT NAME^000000 of player you want to delete.";
					input .Name$;
					callfunc "F_DeleteRecordbyName", .Name$;
				}
			next;
		break;
	}
}

OnReady:
	sleep 3000;
	for( set .@i,0; .@i < getarraysize( .Instruction$ ); set .@i,.@i + 1 ){
		mapannounce .Map$,.Instruction$[.@i],0,0x00FF00;
		sleep 3500;
	}
	for( set .@i,5; .@i > 0; set .@i,.@i - 1 ){
		mapannounce .Map$,"Cuenta regresiva : "+.@i+" segundos.",0,0x00FF00;
		sleep 1000;
	}	
	monster .Map$,29,28,"Breaker Test",1288,1,strnpcinfo(0)+"::OnBreaked";
	set .Record,0;
	sleep 500;
	initnpctimer;
end;

OnTimer600000:
	if( !mobcount( .Map$,strnpcinfo(0)+"::OnBreaked" ) ) end;
	mapannounce .Map$,"Llevas mucho tiempo aqui, creo que estas AFK, que te valla bien!.",0,0xED1ADC;
	sleep 3000;
	mapwarp .Map$,"izlude",103,142;
	stopnpctimer;
end;

OnBreaked:
	set .Time,getnpctimer(0);
	set LatestRecord,.Time;
	announce "Tiempo tomado : "+.Time/1000+"."+.Time%1000+" Seconds. ",bc_self,0xED1ADC;
	stopnpctimer;
	callfunc "F_InsertEmpeTest",strcharinfo(0), .Time;	
	sleep 2000;
	mapwarp .Map$,"prontera",144,187;
	end;
}

function	script	F_GetBest5RompeEmpe	{
	.@query$ = "SELECT char_name, time FROM record_emperium_test WHERE available = 1 ORDER BY time ASC LIMIT 5 ";
	set .@nRows, query_sql(.@query$, .@Name$, .RankTime);
	for(set .@i,0; .@i < .@nRows; set .@i,.@i+1){
		mes "^FF0000Top "+( .@i + 1 )+" : ^0000FF"+.@Name$[.@i]+" ";
		mes "^FF0000Tiempo : ^ED1ADC[ "+.RankTime[.@i]/1000+"."+.RankTime[.@i]%1000+" Sec. ]^000000 ";
	}	
	return;
}

function	script	F_InsertEmpeTest	{
	.@aPlayerName$ = getarg(0);
	.@nTime = getarg(1);
	.@query$ = "INSERT INTO record_emperium_test (char_name, time, date_record) VALUES('"+.@aPlayerName$+"',"+.@nTime+", NOW())";
	query_sql(.@query$, .@Record);
	sleep 100;
	query_sql("SELECT LAST_INSERT_ID();", .lastInserId);
	sleep 100;
	.@query$ = "SELECT id_emperium_test, char_name, time FROM record_emperium_test WHERE available = 1 ORDER BY time ASC LIMIT 5";
	set .@nRows, query_sql(.@query$, .nId, .@Name$, .RankTime);
	sleep 100;
	for(set .@i,0; .@i < .@nRows; set .@i,.@i+1){
		if(.nId[.@i] == .lastInserId){
			announce "[ "+.@Name$[.@i]+" ] Tomo el ranking "+( .@i + 1 )+" en la sala de Rompe Emperium con un record de "+(.RankTime[.@i]/1000)+"."+(.RankTime[.@i]%1000)+" segundos!!!",bc_all,0xED1ADC;
			break;
		}
	}
	return;
}

function	script	F_ResetRecords	{
	.@query$ = "UPDATE record_emperium_test SET available = 0, date_reset = NOW() WHERE available = 1";
	query_sql(.@query$, .@Record);
	return;
}

function	script	F_DeleteRecordbyName	{
	.@query$ = "UPDATE record_emperium_test SET available = 0, date_delete = NOW() WHERE available = 1 AND UPPER(char_name) = UPPER('"+getarg(0)+"')";
	query_sql(.@query$, .@Record);
	return;
}
	
job3_arch01	mapflag	nocommand	5
job3_arch01	mapflag	gvg	on
job3_arch01	mapflag	gvg_castle
job3_arch01	mapflag	monster_noteleport
job3_arch01	mapflag	nosave	SavePoint

//======================================================
// TABLE
//======================================================
//CREATE TABLE `record_emperium_test` (
//  `id_emperium_test` int(11) NOT NULL AUTO_INCREMENT,
//  `char_name` varchar(255) DEFAULT NULL,
//  `time` float DEFAULT NULL,
//  `date_record` datetime DEFAULT NULL,
//  `date_reset` datetime DEFAULT NULL,
//  `date_delete` datetime DEFAULT NULL,
//  `available` tinyint(4) DEFAULT '1',
//  PRIMARY KEY (`id_emperium_test`)
//) ENGINE=InnoDB AUTO_INCREMENT=19 DEFAULT CHARSET=utf8;
//======================================================
